pseudo testall 'exec /zada/olivine/testall.olv'
pseudo xclear 'clear -x'
pseudo pwd 'echo !path'

FOR dir '/bin/commands' '/bin/fatpath'
    FOR e !(find -f !dir)
        pseudo !(name !e) 'go !e'
    END
END

FUNC help
    IF !#
        !0 -h
        RETURN
    END
    echo -e < /zada/common/shell_help.txt
END

FUNC args
    echo called with !# arguments
    FOR i !(range !#)
        echo '!i': !!i
    END
END

FUNC tree
    IF !(eval !# = 0)
        set 0 .
    END

    print !0'\n'

    FOR d !(find -d !0)
        tree !d
    END

    FOR f !(find -f !0)
        print !f'\n'
    END
END

FUNC dirsize
    set size 0
    set level 0

    IF !(eval !# = 0)
        set 0 !path
    END

    IF !(eval !# = 2)
        set level '!1'
    END

    FOR d !(find -d !0)
        set size !(eval !size + !(dirsize !d !(eval !level + 1)))
    END

    FOR f !(find -f !0)
        set size !(eval !size + !(fsize !f))
    END

    IF !level
        RETURN !size
    END

    IF !(eval !size > 10000)
        set size !(eval !size / 1000 . kB)
    END; ELSE
        set size !(eval !size . B)
    END
    echo '!0': !size
END

FUNC time
    IF !(eval !# ~ 1)
        echo Usage: time <command>
        RETURN
    END

    set debut !(ticks)

    !0

    echo !0 took !(eval !(ticks) - !debut) ms
END

FUNC mv
    IF !(eval !# ~ 2)
        echo Usage: mv <source> <destination>
        RETURN
    END

    cp !0 !1
    IF !(eval !exit = 0)
        rm !0
    END
END
